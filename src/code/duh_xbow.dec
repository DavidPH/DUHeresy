//-----------------------------------------------------------------------------
//
// Copyright(C) 2012 David Hill
//
// This program is free software; you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation; either version 3 of the License, or
// (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with this program; if not, see <http://www.gnu.org/licenses/>.
//
//-----------------------------------------------------------------------------
//
// Crossbow actors.
//
//-----------------------------------------------------------------------------


//----------------------------------------------------------------------------|
// Types                                                                      |
//

//
// DUH_CrossbowMissile1
//
actor DUH_CrossbowMissile1 : CrossbowFX3
{
   Damage 10
   SeeSound "weapons/bowshoot"
   Speed 30

   +RIPPER
}

//
// DUH_CrossbowMissile2
//
actor DUH_CrossbowMissile2 : CrossbowFX1
{
   Damage 0
   SeeSound ""
   Speed 30
}

//
// DUH_CrossbowMissile3
//
actor DUH_CrossbowMissile3 : CrossbowFX2
{
   Damage 0
   SeeSound "weapons/bowshoot"
   Speed 30

   +RIPPER
}

//
// DUH_Crossbow
//
actor DUH_Crossbow : Crossbow replaces Crossbow
{
   Weapon.AmmoUse 0
   Weapon.SisterWeapon "DUH_CrossbowPowered"

   states
   {
   Ready:
      CRBW AAAAAA 1 A_WeaponReady
      TNT1 A 0 ACS_NamedExecuteAlways("DUH_CrossbowReady",0, 6)
      CRBW BBBBBB 1 A_WeaponReady
      TNT1 A 0 ACS_NamedExecuteAlways("DUH_CrossbowReady",0, 6)
      CRBW CCCCCC 1 A_WeaponReady
      TNT1 A 0 ACS_NamedExecuteAlways("DUH_CrossbowReady",0, 6)
      goto Ready

   Fire:
      CRBW D 5 A_FireCustomMissile("DUH_CrossbowMissile1")
      CRBW EFGH 2
      CRBW AB 3
      CRBW C 4 A_ReFire
      goto Ready

   AltFire:
      CRBW D 6 ACS_NamedExecuteAlways("DUH_CrossbowAltFire",0, true)
      CRBW EFGH 3
      CRBW AB 4
      CRBW C 5 A_ReFire
      goto Ready
   }
}

//
// DUH_CrossbowPowered
//
actor DUH_CrossbowPowered : DUH_Crossbow replaces CrossbowPowered
{
   Weapon.AmmoGive 0
   Weapon.SisterWeapon "DUH_Crossbow"

   +WEAPON.POWERED_UP

   states
   {
   Ready:
      CRBW AA 1 A_WeaponReady
      TNT1 A 0 ACS_NamedExecuteAlways("DUH_CrossbowReady",0, 2)
      CRBW BB 1 A_WeaponReady
      TNT1 A 0 ACS_NamedExecuteAlways("DUH_CrossbowReady",0, 2)
      CRBW CC 1 A_WeaponReady
      TNT1 A 0 ACS_NamedExecuteAlways("DUH_CrossbowReady",0, 2)
      goto Ready

   Fire:
      TNT1 A 0 ACS_NamedExecuteAlways("DUH_CrossbowReady",0, 15)
      CRBW D 4 A_FireCustomMissile("DUH_CrossbowMissile1")
      CRBW EFGH 1
      CRBW AB 2
      CRBW C 3 A_ReFire
      goto Ready

   AltFire:
      CRBW D 6 ACS_NamedExecuteAlways("DUH_CrossbowAltFire",0, false)
      CRBW EFGH 3
      CRBW AB 4
      CRBW C 5 A_ReFire
      goto Ready
   }
}

// EOF

