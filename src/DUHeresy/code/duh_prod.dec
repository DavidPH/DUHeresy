//-----------------------------------------------------------------------------
//
// Copyright (C) 2012-2017 David Hill
//
// See COPYING for license information.
//
//-----------------------------------------------------------------------------
//
// Phoenix Rod actors.
//
//-----------------------------------------------------------------------------


//----------------------------------------------------------------------------|
// Types                                                                      |
//

//
// DUH_PhoenixRodFireAmmo
//
actor DUH_PhoenixRodFireAmmo : Inventory
{
   Inventory.MaxAmount 41
}

//
// DUH_PhoenixRodFireDamage
//
actor DUH_PhoenixRodFireDamage : Inventory
{
   Inventory.MaxAmount 21
}

//
// DUH_PhoenixRodFireEnd
//
actor DUH_PhoenixRodFireEnd : Inventory
{
   Inventory.MaxAmount 1
}

//
// DUH_PhoenixRodFireTics
//
actor DUH_PhoenixRodFireTics : Inventory
{
   Inventory.MaxAmount 350
}

//
// DUH_PhoenixRodMissile1
//
actor DUH_PhoenixRodMissile1 : PhoenixFX1
{
}

//
// DUH_PhoenixRodMissile2
//
actor DUH_PhoenixRodMissile2 : PhoenixFX2
{
   Damage 0

   +RIPPER

   states
   {
   Spawn:
      TNT1 A 2 // Be invisible for a few tics to get out of the player's face.
   Idle:
      FX09 AB 2 BRIGHT
      FX09 A 0 ACS_NamedExecuteAlways("DUH_PhoenixRodFlameTic",0)
      goto Idle

   Done:
      FX09 CDEF 2 BRIGHT
      stop
   }
}

//
// DUH_PhoenixRod
//
actor DUH_PhoenixRod : PhoenixRod replaces PhoenixRod
{
   Weapon.AmmoUse 0
   Weapon.SisterWeapon "DUH_PhoenixRodPowered"

   states
   {
   Ready:
      PHNX A 1 A_WeaponReady
      PHNX A 0 ACS_NamedExecuteAlways("DUH_PhoenixRodReady",0, 1)
      goto Ready

   Fire:
      PHNX B 5
      PHNX C 0 A_Recoil(4)
      PHNX C 7 A_FireCustomMissile("DUH_PhoenixRodMissile1")
      PHNX DB 4
      PHNX A 0 A_ReFire
      goto Ready

   AltFire:
      PHNX B 3 ACS_NamedExecuteAlways("DUH_PhoenixRodAltInit",0)
   AltHold:
      PHNX C 1 ACS_NamedExecuteAlways("DUH_PhoenixRodAltFire",0)
      PHNX B 4 A_ReFire
   Powerdown:
      PHNX B 4 ACS_NamedExecuteAlways("DUH_PhoenixRodAltDone",0, true)
      goto Ready
   }
}

//
// DUH_PhoenixRodPowered
//
actor DUH_PhoenixRodPowered : DUH_PhoenixRod replaces PhoenixRodPowered
{
   Weapon.AmmoGive 0
   Weapon.SisterWeapon "DUH_PhoenixRod"

   +WEAPON.POWERED_UP

   states
   {
   Fire:
      PHNX B 0 ACS_NamedExecuteAlways("DUH_PhoenixRodReady",0, 11)
      PHNX B 3
      PHNX C 0 A_Recoil(2)
      PHNX C 4 A_FireCustomMissile("DUH_PhoenixRodMissile1")
      PHNX DB 2
      PHNX A 0 A_ReFire
      goto Ready

   AltFire:
      PHNX B 3 ACS_NamedExecuteAlways("DUH_PhoenixRodAltInit",0)
   AltHold:
      PHNX C 1 ACS_NamedExecuteAlways("DUH_PhoenixRodAltFire",0)
      PHNX B 4 A_ReFire
   Powerdown:
      PHNX B 4 ACS_NamedExecuteAlways("DUH_PhoenixRodAltDone",0, false)
      goto Ready
   }
}

// EOF

