//DS---------------------------------------------------------------------------
//
// Copyright(C) 2012 David Hill
//
// This program is free software; you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation; either version 3 of the License, or
// (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with this program; if not, see <http://www.gnu.org/licenses/>.
//
//-----------------------------------------------------------------------------
//
// Elven Wand functions.
//
//-----------------------------------------------------------------------------

#include "duh_arti.dh"

#include "du_defs.dh"


//----------------------------------------------------------------------------|
// Global Functions                                                           |
//

//
// DUH_ElvenLaserTrailSpawn
//
script DUH_ElvenLaserTrailSpawn(bool final)
{
   constexpr int density = 32;

   fixed curx = GetActorX(0);
   fixed cury = GetActorY(0);
   fixed curz = GetActorZ(0);

   fixed oldx = DU_GetInventoryFixed(0, "DUH_MemberOldX");
   fixed oldy = DU_GetInventoryFixed(0, "DUH_MemberOldY");
   fixed oldz = DU_GetInventoryFixed(0, "DUH_MemberOldZ");

   fixed difx = curx - oldx;
   fixed dify = cury - oldy;
   fixed difz = curz - oldz;

   int i = (DU_AbsFixed(difx) + DU_AbsFixed(dify) + DU_AbsFixed(difz)) / density;

   if (i)
   {
      difx = difx / i;
      dify = dify / i;
      difz = difz / i;
   };

   while (i--)
   {
      Spawn("DUH_ElvenLaserTrail", oldx += difx, oldy += dify, oldz += difz, 0,
            GetActorAngle(0)*256);
   };

   if (final)
   {
      if (GetActorZ(0) + 4 >= GetActorCeilingZ(0) &&
          CheckActorCeilingTexture(0, "F_SKY1"))
      {
         SetActorState(0, "DeathSky", false);
      }
      else if (GetActorZ(0) < GetActorFloorZ(0) &&
               CheckActorFloorTexture(0, "F_SKY1"))
      {
         SetActorState(0, "DeathSky", false);
      };
   }
   else
   {
      DU_SetInventoryFixed(0, "DUH_MemberOldX", curx);
      DU_SetInventoryFixed(0, "DUH_MemberOldY", cury);
      DU_SetInventoryFixed(0, "DUH_MemberOldZ", curz);
   };
};

//
// DUH_ElvenWandDamage
//
script DUH_ElvenWandDamage(bool useammo) -> int
{
   int ammo_count = CheckInventory(DUHelvenWandAmmo);
   int ammo_usage = (ammo_count / 50) + 1;
   int damage = (ammo_count / 25) + ammo_usage + 1;

   if (useammo)
      TakeInventory(DUHelvenWandAmmo, ammo_usage);

   return damage;
};

//
// DUH_ElvenWandFire
//
script DUH_ElvenWandFire(bool piercing)
{
   str missile = piercing ? "DUH_ElvenLaserRipper" : "DUH_ElvenLaser";

   int tid = DU_FireMissile(ActivatorTID(), missile, 1, GetActorAngle(0),
      GetActorPitch(0), 256);

   DU_SetInventoryFixed(tid, "DUH_MemberOldX", GetActorX(tid));
   DU_SetInventoryFixed(tid, "DUH_MemberOldY", GetActorY(tid));
   DU_SetInventoryFixed(tid, "DUH_MemberOldZ", GetActorZ(tid));
};

//
// DUH_ElvenWandReady
//
script DUH_ElvenWandReady(int frames)
{
   static int[MAX_PLAYERS] tics;

   DUH_WeaponReady(frames, tics, DUHelvenWandAmmo, 2);
};

// EOF

