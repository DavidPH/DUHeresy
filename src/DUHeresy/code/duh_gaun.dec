//-----------------------------------------------------------------------------
//
// Copyright (C) 2012-2015 David Hill
//
// See COPYING for license information.
//
//-----------------------------------------------------------------------------
//
// Gauntlets actors.
//
//-----------------------------------------------------------------------------


//----------------------------------------------------------------------------|
// Types                                                                      |
//

//
// DUH_Gauntlets
//
actor DUH_Gauntlets : Gauntlets replaces Gauntlets
{
   Weapon.SisterWeapon "DUH_GauntletsPowered"

   -WEAPON.WIMPY_WEAPON

   states
   {
   Ready:
      GAUN A 1 A_WeaponReady
      GAUN A 0 ACS_NamedExecuteAlways("DUH_ArtifactReady",0, 1)
      goto Ready

   Fire:
      GAUN B 4 A_PlayWeaponSound("weapons/gauntletsuse")
      GAUN C 4
   Hold:
      GAUN D 4 BRIGHT A_GauntletAttack(1)
      GAUN EF 4 BRIGHT A_GauntletAttack(0)
      GAUN C 4 A_ReFire
      GAUN B 4 A_Light0
      goto Ready

   AltFire:
      GAUN B 4 A_Light2
      GAUN C 4
   AltHold:
      GAUN DDDD 0 ACS_NamedExecuteAlways("DUH_ArtifactReady",0, 12)
      GAUN DEF 4
      GAUN C 4 A_ReFire
      GAUN B 4 A_Light0
      goto Ready
   }
}

//
// DUH_GauntletsPowered
//
actor DUH_GauntletsPowered : DUH_Gauntlets replaces GauntletsPowered
{
   Weapon.SisterWeapon "DUH_Gauntlets"

   +WEAPON.POWERED_UP

   states
   {
   Ready:
      GAUN GGGG 1 A_WeaponReady
      GAUN H 0 ACS_NamedExecuteAlways("DUH_ArtifactReady",0, 4)
      GAUN HHHH 1 A_WeaponReady
      GAUN I 0 ACS_NamedExecuteAlways("DUH_ArtifactReady",0, 4)
      GAUN IIII 1 A_WeaponReady
      GAUN G 0 ACS_NamedExecuteAlways("DUH_ArtifactReady",0, 4)
      goto Ready

   Fire:
      GAUN J 4 A_PlayWeaponSound("weapons/gauntletsuse")
      GAUN K 4
   Hold:
      GAUN L 0 ACS_NamedExecuteAlways("DUH_ArtifactReady",0, 12)
      GAUN LL 2 BRIGHT A_GauntletAttack(1)
      GAUN MMNN 2 BRIGHT A_GauntletAttack(0)
      GAUN K 4 A_ReFire
      GAUN J 4 A_Light0
      goto Ready

   AltFire:
      GAUN J 4 A_Light2
      GAUN K 4
   AltHold:
      GAUN LLLLLLLL 0 ACS_NamedExecuteAlways("DUH_ArtifactReady",0, 12)
      GAUN LMN 4
      GAUN K 4 A_ReFire
      GAUN J 4 A_Light0
     goto Ready
   }
}

// EOF

