//-----------------------------------------------------------------------------
//
// Copyright (C) 2012-2015 David Hill
//
// See COPYING for license information.
//
//-----------------------------------------------------------------------------
//
// Staff actors.
//
//-----------------------------------------------------------------------------


//----------------------------------------------------------------------------|
// Types                                                                      |
//

//
// DUH_Staff
//
actor DUH_Staff : Staff replaces Staff
{
   Weapon.SisterWeapon "DUH_StaffPowered"

   -WEAPON.WIMPY_WEAPON

   states
   {
   Ready:
      STFF A 1 A_WeaponReady
      STFF A 0 ACS_NamedExecuteAlways("DUH_ArtifactReady",0, 2)
      goto Ready

   Fire:
      STFF B 0 ACS_NamedExecuteAlways("DUH_ArtifactReady",0, 14)
      STFF B 6
      STFF C 8 A_CustomPunch(5, false, 0, "StaffPuff")
      STFF B 8 A_ReFire
      goto Ready

   AltFire:
      STFF A 3
      STFF B 4 A_CustomPunch(2, false, 0, "StaffPuff")
      STFF A 3
      STFF B 4 A_CustomPunch(2, false, 0, "StaffPuff")
      STFF A 3
      STFF B 4 A_CustomPunch(2, false, 0, "StaffPuff")
      STFF AB 3
      STFF C 8 A_CustomPunch(3, false, 0, "StaffPuff")
      STFF B 8 A_ReFire
      goto Ready
   }
}

//
// DUH_StaffPowered
//
actor DUH_StaffPowered : DUH_Staff replaces StaffPowered
{
   Weapon.SisterWeapon "DUH_Staff"

   +WEAPON.POWERED_UP

   states
   {
   Ready:
      STFF DDDD 1 A_WeaponReady
      STFF EEEE 0 ACS_NamedExecuteAlways("DUH_ArtifactReady",0, 4)
      STFF EEEE 1 A_WeaponReady
      STFF FFFF 0 ACS_NamedExecuteAlways("DUH_ArtifactReady",0, 4)
      STFF FFFF 1 A_WeaponReady
      STFF DDDD 0 ACS_NamedExecuteAlways("DUH_ArtifactReady",0, 4)
      goto Ready

   Fire:
      STFF GG 0 ACS_NamedExecuteAlways("DUH_ArtifactReady",0, 14)
      STFF G 6
      STFF H 8 A_CustomPunch(15, false, 0, "StaffPuff2")
      STFF G 8 A_ReFire
      goto Ready

   AltFire:
      STFF F 0 ACS_NamedExecuteAlways("DUH_ArtifactReady",0, 32)
      STFF F 3
      STFF G 4 A_CustomPunch(6, false, 0, "StaffPuff2")
      STFF F 3
      STFF G 4 A_CustomPunch(6, false, 0, "StaffPuff2")
      STFF F 3
      STFF G 4 A_CustomPunch(6, false, 0, "StaffPuff2")
      STFF FG 3
      STFF H 8 A_CustomPunch(9, false, 0, "StaffPuff2")
      STFF G 8 A_ReFire
      goto Ready
   }
}

// EOF

