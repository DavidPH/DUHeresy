//-----------------------------------------------------------------------------
//
// Copyright (C) 2012-2015 David Hill
//
// See COPYING for license information.
//
//-----------------------------------------------------------------------------
//
// Artifact actors.
//
//-----------------------------------------------------------------------------


//----------------------------------------------------------------------------|
// Types                                                                      |
//

//
// DUH_ArmorBonus
//
actor DUH_ArmorBonus : BasicArmorBonus
{
   Armor.MaxSaveAmount 200
   Armor.SaveAmount 1
   Armor.SavePercent 75
}

//
// DUH_BagOfHolding
//
actor DUH_BagOfHolding : Inventory replaces BagOfHolding
{
   Inventory.MaxAmount 16
   Inventory.PickupMessage "$TXT_ITEMBAGOFHOLDING"

   +FLOATBOB

   states
   {
   Spawn:
      BAGH A -1
      stop
   }
}

//
// DUH_ChickenFriend
//
actor DUH_ChickenFriend
{
   ActiveSound "chicken/active"
   AttackSound ""
   Damage 0
   DeathSound "chicken/death"
   Game Heretic
   Health 256
   Height 22
   Mass 40
   Obituary "$OB_CHICKEN"
   PainChance 200
   PainSound "chicken/pain"
   Radius 9
   SeeSound "chicken/pain"
   Speed 8

   MONSTER
   +DONTMORPH
   +FLOORCLIP
   +FRIENDLY
   +NORADIUSDMG
   +WINDTHRUST
   -COUNTKILL

   states
   {
   Spawn:
      CHKN AB 10 A_Look
      CHKN A 0 ACS_NamedExecuteAlways("DUH_ChickenFriendCountdown",0, 20)
      goto Spawn

   See:
      CHKN A 0 A_CustomMeleeAttack(Random(5, 9), "chicken/attack")
      CHKN AABB 1 A_Chase
      CHKN A 0 ACS_NamedExecuteAlways("DUH_ChickenFriendCountdown",0, 4)
      goto See

   Pain:
      CHKN D 0 ThrustThing(angle*256/360+128, 2)
      CHKN D 0 ThrustThingZ(0, 32, false, true)
      CHKN D 5 A_Feathers
      CHKN C 5 A_Pain
      goto See

   Melee:
      CHKN A 8 A_FaceTarget
      CHKN C 10 A_CustomMeleeAttack(Random(5, 9), "chicken/attack")
      goto See

   Missile:
      CHKN A 0 ThrustThingZ(0, 64, false, true)
      CHKN A 0 A_Feathers
      CHKN A 8 A_FaceTarget
      CHKN C 0 A_Feathers
      CHKN A 2 A_SkullAttack
      CHKN C 2 A_SkullAttack
      goto Missile+3

   Death:
      CHKN E 6 A_Scream
      CHKN F 6 A_Feathers
      CHKN G 6
      CHKN H 6 A_NoBlocking
      CHKN IJK 6
      CHKN L -1
      stop
   }
}

//
// DUH_ChickenFriendCounter
//
// Used to count down to dealing damage.
//
actor DUH_ChickenFriendCounter : Inventory
{
   Inventory.MaxAmount 100
}

//
// DUH_SilverShield
//
actor DUH_SilverShield : SilverShield replaces SilverShield
{
   Armor.SavePercent 75
}

//
// DUH_TimeBombAura
//
actor DUH_TimeBombAura
{
   PROJECTILE

   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 1 A_Explode(8, 256, false)
      stop
   }
}

// EOF

