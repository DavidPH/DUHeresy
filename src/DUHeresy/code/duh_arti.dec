//-----------------------------------------------------------------------------
//
// Copyright(C) 2012 David Hill
//
// This program is free software; you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation; either version 3 of the License, or
// (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.
//
// You should have received a copy of the GNU General Public License
// along with this program; if not, see <http://www.gnu.org/licenses/>.
//
//-----------------------------------------------------------------------------
//
// Artifact actors.
//
//-----------------------------------------------------------------------------


//----------------------------------------------------------------------------|
// Types                                                                      |
//

//
// DUH_ArmorBonus
//
actor DUH_ArmorBonus : BasicArmorBonus
{
   Armor.MaxSaveAmount 200
   Armor.SaveAmount 1
   Armor.SavePercent 75
}

//
// DUH_BagOfHolding
//
actor DUH_BagOfHolding : CustomInventory replaces BagOfHolding
{
   Inventory.PickupMessage "$TXT_ITEMBAGOFHOLDING"

   +COUNTITEM
   +FLOATBOB

   states
   {
   Spawn:
      BAGH A -1
      goto Spawn

   Pickup:
      TNT1 A 0 A_GiveInventory("ArtiTomeOfPower", 2)
      stop
   }
}

//
// DUH_ChickenFriend
//
actor DUH_ChickenFriend : Chicken
{
   Damage 0
   Health 300
   Speed 8

   +FRIENDLY
   +NORADIUSDMG

   states
   {
   Spawn:
      CHKN AB 10 A_Look
      TNT1 A 0 ACS_ExecuteAlways(DUH_ChickenFriendCountdown,0, 20)
      goto Spawn

   See:
      CHKN C 0 A_CustomMeleeAttack(random[ChicAttack](5,9))
      CHKN AABB 1 A_Chase
      TNT1 A 0 ACS_ExecuteAlways(DUH_ChickenFriendCountdown,0, 4)
      goto See

   Pain:
      TNT1 A 0 ThrustThing(angle*256/360+128, 2)
      TNT1 A 0 ThrustThingZ(0, 32, false, true)
      CHKN D 5 A_Feathers
      CHKN C 5 A_Pain
      goto See

   Melee:
      CHKN A 8 A_FaceTarget
      CHKN C 10 A_CustomMeleeAttack(random[ChicAttack](5,9))
      goto See

   Missile:
      CHKN A 0 ThrustThingZ(0, 64, false, true)
      CHKN A 0 A_Feathers
      CHKN A 8 A_FaceTarget
      CHKN C 0 A_Feathers
      CHKN C 2 A_SkullAttack
      goto Missile+3
   }
}

//
// DUH_ChickenFriendCounter
//
// Used to count down to dealing damage.
//
actor DUH_ChickenFriendCounter : Inventory
{
   Inventory.MaxAmount 100
}

//
// DUH_SilverShield
//
actor DUH_SilverShield : SilverShield replaces SilverShield
{
   Armor.SavePercent 75
}

//
// DUH_TimeBombAura
//
actor DUH_TimeBombAura
{
   PROJECTILE

   states
   {
   Spawn:
      TNT1 A 0
      TNT1 A 1 A_Explode(8, 256, false)
      stop
   }
}

// EOF

