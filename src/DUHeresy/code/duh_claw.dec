//-----------------------------------------------------------------------------
//
// Copyright (C) 2012-2015 David Hill
//
// See COPYING for license information.
//
//-----------------------------------------------------------------------------
//
// Dragon Claw actors.
//
//-----------------------------------------------------------------------------


//----------------------------------------------------------------------------|
// Types                                                                      |
//

//
// DUH_DragonClaw
//
actor DUH_DragonClaw : Blaster replaces Blaster
{
   AttackSound "weapons/blastershoot"

   Weapon.AmmoUse 0
   Weapon.SisterWeapon "DUH_DragonClawPowered"

   states
   {
   Ready:
      BLSR A 1 A_WeaponReady
      BLSR A 0 ACS_NamedExecuteAlways("DUH_DragonClawReady",0, 1)
      goto Ready

   Fire:
      BLSR BC 3
   Hold:
      BLSR D 2 A_FireBullets(5.0, 5.0, -1, Random(1, 8) + Random(1, 8),
         "BlasterPuff", FBF_NORANDOM)
      BLSR CB 2
      BLSR A 0 A_ReFire
      goto Ready

   AltFire:
      BLSR BC 3
   AltHold:
      BLSR D 2 A_FireBullets(3.0, 3.0, -1,
         CallACS("DUH_DragonClawDamage", true), "BlasterPuff", FBF_NORANDOM)
      BLSR CB 2
      BLSR A 0 A_ReFire
      goto Ready
   }
}

//
// DUH_DragonClawPowered
//
actor DUH_DragonClawPowered : DUH_DragonClaw replaces BlasterPowered
{
   Weapon.AmmoGive 0
   Weapon.SisterWeapon "DUH_DragonClaw"

   +WEAPON.POWERED_UP

   states
   {
   Fire:
      BLSR BC 3
   Hold:
      BLSR D 0 ACS_NamedExecuteAlways("DUH_DragonClawReady",0, 4)
      BLSR D 1 A_FireBullets(5.0, 5.0, -1, Random(1, 8) + Random(1, 8),
         "BlasterPuff", FBF_NORANDOM)
      BLSR CB 1
      BLSR A 0 A_ReFire
      goto Ready

   AltFire:
      BLSR BC 3
   AltHold:
      BLSR D 2 A_FireBullets(3.0, 3.0, -1,
         CallACS("DUH_DragonClawDamage", false), "BlasterPuff", FBF_NORANDOM)
      BLSR CB 2
      BLSR A 0 A_ReFire
      goto Ready
   }
}

// EOF

