//-----------------------------------------------------------------------------
//
// Copyright (C) 2012-2015 David Hill
//
// See COPYING for license information.
//
//-----------------------------------------------------------------------------
//
// Crossbow actors.
//
//-----------------------------------------------------------------------------


//----------------------------------------------------------------------------|
// Types                                                                      |
//

//
// DUH_CrossbowMissile1
//
actor DUH_CrossbowMissile1 : CrossbowFX3
{
   Damage 10
   SeeSound "weapons/bowshoot"
   Speed 30
}

//
// DUH_CrossbowMissile2
//
actor DUH_CrossbowMissile2 : CrossbowFX1
{
   Damage 0
   SeeSound ""
   Speed 30
}

//
// DUH_CrossbowMissile3
//
actor DUH_CrossbowMissile3 : CrossbowFX2
{
   Damage 0
   SeeSound "weapons/bowshoot"
   Speed 30

   +RIPPER
}

//
// DUH_Crossbow
//
actor DUH_Crossbow : Crossbow replaces Crossbow
{
   Weapon.AmmoUse 0
   Weapon.SisterWeapon "DUH_CrossbowPowered"

   states
   {
   Ready:
      CRBW AAAAAA 1 A_WeaponReady
      CRBW B 0 ACS_NamedExecuteAlways("DUH_CrossbowReady",0, 6)
      CRBW BBBBBB 1 A_WeaponReady
      CRBW C 0 ACS_NamedExecuteAlways("DUH_CrossbowReady",0, 6)
      CRBW CCCCCC 1 A_WeaponReady
      CRBW A 0 ACS_NamedExecuteAlways("DUH_CrossbowReady",0, 6)
      goto Ready

   Fire:
      CRBW D 5 A_FireCustomMissile("DUH_CrossbowMissile1")
      CRBW EFGH 2
      CRBW AB 3
      CRBW C 4 A_ReFire
      goto Ready

   AltFire:
      CRBW D 6 ACS_NamedExecuteAlways("DUH_CrossbowAltFire",0, true)
      CRBW EFGH 3
      CRBW AB 4
      CRBW C 5 A_ReFire
      goto Ready
   }
}

//
// DUH_CrossbowPowered
//
actor DUH_CrossbowPowered : DUH_Crossbow replaces CrossbowPowered
{
   Weapon.AmmoGive 0
   Weapon.SisterWeapon "DUH_Crossbow"

   +WEAPON.POWERED_UP

   states
   {
   Ready:
      CRBW AA 1 A_WeaponReady
      CRBW B 0 ACS_NamedExecuteAlways("DUH_CrossbowReady",0, 2)
      CRBW BB 1 A_WeaponReady
      CRBW C 0 ACS_NamedExecuteAlways("DUH_CrossbowReady",0, 2)
      CRBW CC 1 A_WeaponReady
      CRBW A 0 ACS_NamedExecuteAlways("DUH_CrossbowReady",0, 2)
      goto Ready

   Fire:
      CRBW D 0 ACS_NamedExecuteAlways("DUH_CrossbowReady",0, 15)
      CRBW D 4 A_FireCustomMissile("DUH_CrossbowMissile1")
      CRBW EFGH 1
      CRBW AB 2
      CRBW C 3 A_ReFire
      goto Ready

   AltFire:
      CRBW D 6 ACS_NamedExecuteAlways("DUH_CrossbowAltFire",0, false)
      CRBW EFGH 3
      CRBW AB 4
      CRBW C 5 A_ReFire
      goto Ready
   }
}

// EOF

