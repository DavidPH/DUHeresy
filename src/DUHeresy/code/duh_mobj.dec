//-----------------------------------------------------------------------------
//
// Copyright (C) 2012-2015 David Hill
//
// See COPYING for license information.
//
//-----------------------------------------------------------------------------
//
// Mobj actors.
//
//-----------------------------------------------------------------------------


//----------------------------------------------------------------------------|
// Types                                                                      |
//

//
// DUH_Disciple
//
actor DUH_Disciple : Wizard replaces Wizard
{
   states
   {
   Death:
      WZRD F 0 ACS_NamedExecuteWithResult("DUH_ThingDeath", 180)
      WZRD F 6 A_GhostOff
      WZRD G 6 A_Scream
      WZRD HI 6
      WZRD J 6 A_NoBlocking
      WZRD KLM 6
      WZRD M 6 A_SetFloorClip
      WZRD M -1 ACS_NamedExecuteAlways("DUH_RaiseThingDelayed",0,
         Random(2800, 3150))
      stop

   Raise:
      WZRD M 12 A_UnSetFloorClip
      WZRD LKJIHGF 12
      goto See
   }
}

//
// DUH_DSparil
//
actor DUH_DSparil : Sorcerer2 replaces Sorcerer2
{
   Health 8000

   states
   {
   Melee:
      SOR2 R 3 A_Srcr2Decide
      SOR2 R 3 A_Srcr2Decide
      SOR2 R 3 A_Srcr2Decide
      SOR2 S 9 A_FaceTarget
      SOR2 T 9 A_CustomMeleeAttack(20 * Random(1, 8), "dsparil/attack")
      goto See

   Missile:
      SOR2 R 9 A_Srcr2Decide
      SOR2 S 9 A_FaceTarget
      SOR2 T 9 A_Srcr2Attack
      goto See

   Pain:
      SOR2 Q 3
      SOR2 Q 3 A_Srcr2Decide
      SOR2 Q 3 A_Pain
      goto See

   Death:
      SDTH A 0 ACS_NamedExecuteWithResult("DUH_ThingDeath", 8000)
      SDTH AB 8
      SDTH C 8 A_PlaySoundEx("dsparil/scream", "Body")
      SDTH DEFDEFDEFDEFDEFDEFDEF 7
      SDTH G 6 A_PlaySoundEx("dsparil/explode", "Body")
      SDTH HIII 6
      SDTH J 6 A_NoBlocking
      SDTH K 6 A_PlaySoundEx("dsparil/bones", "Body")
      SDTH LMN 6
      SDTH O 6 ACS_NamedExecuteWithResult("DUH_DSparilDeath")
      SDTH O -1 ACS_NamedExecuteAlways("DUH_RaiseThingDelayed",0, Random(175, 350))
      stop

   Raise:
      SDTH O 12 A_SetInvulnerable
      SDTH N 12 A_PlaySoundEx("dsparil/bones", "Body")
      SDTH MLKJIIIH 12
      SDTH G 12 A_PlaySoundEx("dsparil/scream", "Body")
      SDTH FEDFEDFEDFEDFEDFEDFED 14
      SDTH C 16 A_PlaySoundEx("dsparil/explode", "Body")
      SDTH B 16
      SDTH A 16 A_UnSetInvulnerable
      goto See
   }
}

//
// DUH_DSparilMounted
//
actor DUH_DSparilMounted : Sorcerer1 replaces Sorcerer1
{
   Health 4000
}

//
// DUH_FireGargoyle
//
actor DUH_FireGargoyle : HereticImpLeader replaces HereticImpLeader
{
   states
   {
   Death:
      IMPX G 0 ACS_NamedExecuteWithResult("DUH_ThingDeath", 80)
      IMPX G 4 A_ImpDeath
      IMPX H 5
      wait

   XDeath:
      IMPX S 0 ACS_NamedExecuteWithResult("DUH_ThingDeath", 80)
      IMPX S 5 A_ImpXDeath1
      IMPX TU 5
      IMPX V 5 A_Gravity
      IMPX W 5
      wait

   Crash:
      IMPX I 7 A_ImpExplode
      IMPX J 7 A_Scream
      IMPX KL 7
      IMPX L -1 ACS_NamedExecuteAlways("DUH_RaiseThingDelayed",0,
         Random(700, 1050))
      stop

   XCrash:
      IMPX X 7
      IMPX YZ 7
      IMPX Z -1 ACS_NamedExecuteAlways("DUH_RaiseThingDelayed",0,
         Random(700, 1050))
      stop

   Raise:
      IMPX LKJIHG 10
      goto See
   }
}

//
// DUH_Gargoyle
//
actor DUH_Gargoyle : HereticImp replaces HereticImp
{
   states
   {
   Death:
      IMPX G 0 ACS_NamedExecuteWithResult("DUH_ThingDeath", 40)
      IMPX G 4 A_ImpDeath
      IMPX H 5
      wait

   XDeath:
      IMPX S 0 ACS_NamedExecuteWithResult("DUH_ThingDeath", 40)
      IMPX S 5 A_ImpXDeath1
      IMPX TU 5
      IMPX V 5 A_Gravity
      IMPX W 5
      wait

   Crash:
      IMPX I 7 A_ImpExplode
      IMPX J 7 A_Scream
      IMPX KL 7
      IMPX L -1 ACS_NamedExecuteAlways("DUH_RaiseThingDelayed",0,
         Random(350, 700))
      stop

   XCrash:
      IMPX X 7
      IMPX YZ 7
      IMPX Z -1 ACS_NamedExecuteAlways("DUH_RaiseThingDelayed",0,
         Random(350, 700))
      stop

   Raise:
      IMPX LKJIHG 10
      goto See
   }
}

//
// DUH_Golem
//
actor DUH_Golem : Mummy replaces Mummy
{
   states
   {
   Death:
      MUMM I 0 ACS_NamedExecuteWithResult("DUH_ThingDeath", 80)
      MUMM I 5
      MUMM J 5 A_Scream
      MUMM K 5 A_SpawnItemEx("MummySoul", 0,0,10, 0,0,1)
      MUMM L 5
      MUMM M 5 A_NoBlocking
      MUMM NOP 5
      MUMM P -1 ACS_NamedExecuteAlways("DUH_RaiseThingDelayed",0,
         Random(700, 1050))
      stop

   Raise:
      MUMM ONMLKJI 10
      goto See
   }
}

//
// DUH_GhostGolem
//
actor DUH_GhostGolem : DUH_Golem replaces MummyGhost
{
   Alpha 0.4
   RenderStyle Translucent

   +GHOST
   +SHADOW
}

//
// DUH_IronLich
//
actor DUH_IronLich : IronLich replaces IronLich
{
   states
   {
   Death:
      LICH C 0 ACS_NamedExecuteWithResult("DUH_ThingDeath", 700)
      LICH C 7
      LICH D 7 A_Scream
      LICH EF 7
      LICH G 7 A_NoBlocking
      LICH H 7
      LICH I 7 A_BossDeath
      LICH I -1 ACS_NamedExecuteAlways("DUH_RaiseThingDelayed",0,
         Random(3150, 3500))
      stop

   Raise:
      LICH IHGFEDC 14
      goto See
   }
}

//
// DUH_Maulotaur
//
actor DUH_Maulotaur : Minotaur replaces Minotaur
{
   states
   {
   Death:
      MNTR F 0 ACS_NamedExecuteWithResult("DUH_ThingDeath", 3000)
      MNTR F 6 A_MinotaurDeath
      MNTR G 5
      MNTR H 6 A_Scream
      MNTR I 5
      MNTR J 6
      MNTR K 5
      MNTR L 6
      MNTR M 5 A_NoBlocking
      MNTR N 6
      MNTR O 5
      MNTR P 6
      MNTR Q 5
      MNTR R 6
      MNTR S 5
      MNTR T 6 A_BossDeath
      MNTR T -1 ACS_NamedExecuteAlways("DUH_RaiseThingDelayed",0,
         Random(3500, 3850))
      stop

   Raise:
      MNTR TSRQPONMLKJIHGF 12
      goto See
   }
}

//
// DUH_Nitrogolem
//
actor DUH_Nitrogolem : MummyLeader replaces MummyLeader
{
   states
   {
   Death:
      MUMM I 0 ACS_NamedExecuteWithResult("DUH_ThingDeath", 100)
      MUMM I 5
      MUMM J 5 A_Scream
      MUMM K 5 A_SpawnItemEx("MummySoul", 0,0,10, 0,0,1)
      MUMM L 5
      MUMM M 5 A_NoBlocking
      MUMM NOP 5
      MUMM P -1 ACS_NamedExecuteAlways("DUH_RaiseThingDelayed",0,
         Random(1050, 1400))
      stop

   Raise:
      MUMM ONMLKJI 10
      goto See
   }
}

//
// DUH_GhostNitrogolem
//
actor DUH_GhostNitrogolem : DUH_Nitrogolem replaces MummyLeaderGhost
{
   Alpha 0.4
   RenderStyle Translucent

   +GHOST
   +SHADOW
}

//
// DUH_Ophidian
//
actor DUH_Ophidian : Snake replaces Snake
{
   states
   {
   Death:
      SNKE G 0 ACS_NamedExecuteWithResult("DUH_ThingDeath", 280)
      SNKE G 5
      SNKE H 5 A_Scream
      SNKE IJKL 5
      SNKE M 5 A_NoBlocking
      SNKE NOP 5
      SNKE P -1 ACS_NamedExecuteAlways("DUH_RaiseThingDelayed",0,
         Random(2450, 2800))
      stop

   Raise:
     SNKE PONMLKJIHG 10
     goto See
   }
}

//
// DUH_Sabreclaw
//
actor DUH_Sabreclaw : Clink replaces Clink
{
   states
   {
   Death:
      CLNK I 0 ACS_NamedExecuteWithResult("DUH_ThingDeath", 150)
      CLNK IJ 6
      CLNK K 5 A_Scream
      CLNK L 5 A_NoBlocking
      CLNK MNO 5
      CLNK O -1 ACS_NamedExecuteAlways("DUH_RaiseThingDelayed",0,
         Random(1750, 2100))
      stop

   Raise:
      CLNK ONMLKJI 10
      goto See
   }
}

//
// DUH_UndeadWarrior
//
actor DUH_UndeadWarrior : Knight replaces Knight
{
   states
   {
   Death:
      KNIG I 0 ACS_NamedExecuteWithResult("DUH_ThingDeath", 200)
      KNIG I 6
      KNIG J 6 A_Scream
      KNIG K 6
      KNIG L 6 A_NoBlocking
      KNIG MNO 6
      KNIG O -1 ACS_NamedExecuteAlways("DUH_RaiseThingDelayed",0,
         Random(1400, 1750))
      stop

   Raise:
      KNIG ONMLKJI 12
      goto See
   }
}

//
// DUH_GhostWarrior
//
actor DUH_GhostWarrior : DUH_UndeadWarrior replaces KnightGhost
{
   Alpha 0.4
   RenderStyle Translucent

   +GHOST
   +SHADOW
}

//
// DUH_Weredragon
//
actor DUH_Weredragon : Beast replaces Beast
{
   states
   {
   Death:
      BEAS R 0 ACS_NamedExecuteWithResult("DUH_ThingDeath", 220)
      BEAS R 6
      BEAS S 6 A_Scream
      BEAS TUV 6
      BEAS W 6 A_NoBlocking
      BEAS XYZ 6
      BEAS Z -1 ACS_NamedExecuteAlways("DUH_RaiseThingDelayed",0,
         Random(2100, 2450))
      stop

   XDeath:
      BEAS J 0 ACS_NamedExecuteWithResult("DUH_ThingDeath", 220)
      BEAS J 5
      BEAS K 6 A_Scream
      BEAS L 5
      BEAS M 6
      BEAS N 5
      BEAS O 6 A_NoBlocking
      BEAS P 5
      BEAS Q 6
      BEAS Q -1 ACS_NamedExecuteAlways("DUH_RaiseThingDelayed",0,
         Random(2100, 2450))
      stop

   Raise:
      BEAS ZYXWVUTSR 12
      goto See
   }
}

// EOF

