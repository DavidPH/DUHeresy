//-----------------------------------------------------------------------------
//
// Copyright (C) 2012-2015 David Hill
//
// See COPYING for license information.
//
//-----------------------------------------------------------------------------
//
// Firemace actors.
//
//-----------------------------------------------------------------------------


//----------------------------------------------------------------------------|
// Types                                                                      |
//

//
// DUH_FiremaceMissile1
//
actor DUH_FiremaceMissile1 : MaceFX1
{
   Damage 1
   DeathSound "weapons/macehit"
   Gravity 0.125

   +DOOMBOUNCE
   -NOGRAVITY

   states
   {
   Spawn:
      FX02 AA 0 ACS_NamedExecuteAlways("DUH_FiremaceMissileSound",0)
      FX02 A 0 A_Jump(256, "Idle")
   Idle:
      FX02 CD 4 BRIGHT
      goto Idle

   Death:
      FX02 A 0 A_CustomMissile("DUH_FiremaceMissile2", 0, 0, +11.25,
         CMF_AIMDIRECTION|CMF_TRACKOWNER, 22.5)
      FX02 A 0 A_CustomMissile("DUH_FiremaceMissile2", 0, 0, -11.25,
         CMF_AIMDIRECTION|CMF_TRACKOWNER, 22.5)
   DeathDone:
      FX02 FGHIJ 4 BRIGHT
      stop
   }
}

//
// DUH_FiremaceMissile2
//
actor DUH_FiremaceMissile2 : DUH_FiremaceMissile1
{
   Speed 10

   states
   {
   Idle:
      FX02 AB 4 BRIGHT
      goto Idle

   Death:
      FX02 A 0 A_CustomMissile("DUH_FiremaceMissile3", 0, 0, +5.625,
         CMF_AIMDIRECTION|CMF_TRACKOWNER, 11.25)
      FX02 A 0 A_CustomMissile("DUH_FiremaceMissile3", 0, 0, -5.625,
         CMF_AIMDIRECTION|CMF_TRACKOWNER, 11.25)
      goto DeathDone
   }
}

//
// DUH_FiremaceMissile3
//
actor DUH_FiremaceMissile3 : DUH_FiremaceMissile2
{
   Speed 5

   states
   {
   Idle:
      FX02 AB 4
      goto Idle

   Death:
      goto DeathDone
   }
}

//
// DUH_FiremaceMissile4
//
actor DUH_FiremaceMissile4
{
   Damage 0
   DeathSound "weapons/macehit"
   Game Heretic
   Height 6
   Radius 8
   SeeSound "weapons/maceshoot"
   Speed 7

   PROJECTILE

   var int user_shooter;

   states
   {
   Spawn:
      FX02 E -1
      goto Spawn

   Death:
      FX02 F 4 BRIGHT ACS_NamedExecuteAlways("DUH_FiremaceDeathRing",0)
      FX02 GHIJ 4 BRIGHT
      stop
   }
}

//
// DUH_FiremaceMissile5
//
actor DUH_FiremaceMissile5 : DUH_FiremaceMissile4
{
   Damage 1
   Speed 15
   SeeSound ""

   +RIPPER

   states
   {
   Spawn:
      FX02 AB 4
      goto Spawn

   Death:
      FX02 FGHIJ 4 BRIGHT
      stop
   }
}

//
// DUH_Firemace
//
actor DUH_Firemace : Mace replaces Mace
{
   Weapon.AmmoUse 0
   Weapon.SisterWeapon "DUH_FiremacePowered"

   states
   {
   Ready:
      MACE A 1 A_WeaponReady
      MACE A 0 ACS_NamedExecuteAlways("DUH_FiremaceReady",0, 1)
      goto Ready

   Fire:
      MACE B 4
   Hold:
      MACE CDEF 3 A_FireCustomMissile("DUH_FiremaceMissile1")
      MACE C 4 A_ReFire
      MACE DEF 4
      goto Ready

   AltFire:
      MACE B 20 A_PlaySound("dsparil/rise", CHAN_WEAPON)
      MACE C 10
      MACE C 10 ACS_NamedExecuteAlways("DUH_FiremaceAltFire",0, true)
      MACE B 20 A_ReFire
      goto Ready
   }
}

//
// DUH_FiremacePowered
//
actor DUH_FiremacePowered : DUH_Firemace replaces MacePowered
{
   Weapon.AmmoGive 0
   Weapon.SisterWeapon "DUH_Firemace"

   +WEAPON.POWERED_UP

   states
   {
   Fire:
      MACE B 4
   Hold:
      MACE C 0 ACS_NamedExecuteAlways("DUH_FiremaceReady",0, 12)
      MACE CDEF 3 A_FireCustomMissile("DUH_FiremaceMissile1")
      MACE C 4 A_ReFire
      MACE DEF 4
      goto Ready

   AltFire:
      MACE B 20 A_PlaySound("dsparil/rise", CHAN_WEAPON)
      MACE C 10
      MACE C 10 ACS_NamedExecuteAlways("DUH_FiremaceAltFire",0, false)
      MACE B 20 A_ReFire
      goto Ready
   }
}

// EOF

