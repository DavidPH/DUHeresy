//-----------------------------------------------------------------------------
//
// Copyright (C) 2012-2015 David Hill
//
// See COPYING for license information.
//
//-----------------------------------------------------------------------------
//
// Hellstaff actors.
//
//-----------------------------------------------------------------------------


//----------------------------------------------------------------------------|
// Types                                                                      |
//

//
// DUH_HellstaffMissile1
//
actor DUH_HellstaffMissile1 : HornRodFX1
{
}

//
// DUH_HellstaffMissile2
//
actor DUH_HellstaffMissile2 : HornRodFX2
{
   Damage 0

   var int user_shooter;

   states
   {
   Spawn:
      FX00 CDEF 3 BRIGHT
      goto Spawn

   Death:
      FX00 H 5 BRIGHT ACS_NamedExecuteAlways("DUH_HellstaffStorm",0)
      FX00 I 5 BRIGHT
      FX00 J 4 BRIGHT
      FX00 KLM 3 BRIGHT
      stop
   }
}

//
// DUH_HellstaffMissile3
//
actor DUH_HellstaffMissile3 : RainPillar
{
   +SPAWNCEILING
}

//
// DUH_HellstaffMissileStormTics
//
actor DUH_HellstaffMissileStormTics : Inventory
{
   Inventory.MaxAmount 2010
}

//
// DUH_Hellstaff
//
actor DUH_Hellstaff : SkullRod replaces SkullRod
{
   Weapon.AmmoUse 0
   Weapon.SisterWeapon "DUH_HellstaffPowered"

   states
   {
   Ready:
      HROD A 1 A_WeaponReady
      HROD A 0 ACS_NamedExecuteAlways("DUH_HellstaffReady",0, 1)
      goto Ready

   Fire:
      HROD AB 4 A_FireCustomMissile("DUH_HellstaffMissile1")
      HROD A 0 A_ReFire
      goto Ready

   AltFire:
      HROD C 2
      HROD D 3
      HROD E 2
      HROD F 3
      HROD G 4 ACS_NamedExecuteAlways("DUH_HellstaffAltFire",0, true)
      HROD F 2
      HROD E 3
      HROD D 2
      HROD C 2 A_ReFire
      goto Ready
   }
}

//
// DUH_HellstaffPowered
//
actor DUH_HellstaffPowered : DUH_Hellstaff replaces SkullRodPowered
{
   Weapon.AmmoGive 0
   Weapon.SisterWeapon "DUH_Hellstaff"

   +WEAPON.POWERED_UP

   states
   {
   Fire:
      HROD A 0 ACS_NamedExecuteAlways("DUH_HellstaffReady",0, 4)
      HROD AB 2 A_FireCustomMissile("DUH_HellstaffMissile1")
      HROD A 0 A_ReFire
      goto Ready

   AltFire:
      HROD C 2
      HROD D 3
      HROD E 2
      HROD F 3
      HROD G 4 ACS_NamedExecuteAlways("DUH_HellstaffAltFire",0, false)
      HROD F 2
      HROD E 3
      HROD D 2
      HROD C 2 A_ReFire
      goto Ready
   }
}

// EOF

